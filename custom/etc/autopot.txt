//===== rAthena Script =======================================
//= Auto-Potion
//===== Description: =========================================
//= Provides an @autopot command to automatically use potions.
//===== Changelogs: ==========================================
//= 1.0 First version. [Euphy]
//= 2.0 rewrite and adding SP option. [sader1992]
//============================================================
-	script	#autopot	-1,{
OnInit:
	.HP_Option = true; //允許HP選項
	.SP_Option = true; //允許SP選項
	.MinDelay = 100; //允許的最小延遲（單位：毫秒，默認和建議值：100）[較低的值會增加伺服器負擔]
	.MaxDelay = 1000; //允許的最大延遲（單位：毫秒，默認和建議值：1000）
	setarray .blackList[0], 0; //黑名單物品ID的數組

	if(!.HP_Option && !.SP_Option){
		debugmes "警告：@autopot 的HP和SP選項均已禁用";
		debugmes "警告：@autopot 腳本已禁用！";
		end;
	}
	bindatcmd("autopot", strnpcinfo(0) + "::OnCommand", 0, 99);
end;

L_Help:
	dispbottom "可用指令：";
	if(.HP_Option && .SP_Option){
		dispbottom "    @autopot <hp|sp> <物品ID> {<最低HP|SP百分比 [1..100]> {<延遲 [" + .MinDelay + ".."+ .MaxDelay + "]>}}";
		dispbottom "    @autopot <hp|sp> list";
		dispbottom "    @autopot <hp|sp> <on|off>";
	}else{
		dispbottom "    @autopot <" + (.HP_Option ? "hp" : "sp") + "> <物品ID> {<最低" + (.HP_Option ? "HP" : "SP") + "百分比 [1..100]> {<延遲 [" + .MinDelay + ".."+ .MaxDelay + "]>}}";
		dispbottom "    @autopot <" + (.HP_Option ? "hp" : "sp") + "> list";
		dispbottom "    @autopot <" + (.HP_Option ? "hp" : "sp") + "> <on|off>";
	}
	dispbottom "    @autopot info";
	dispbottom "    @autopot help";
	dispbottom "    @autopot blacklist";
	return;

L_Info:
	dispbottom "----------自動藥水資訊-----------";
	if(.HP_Option){
		dispbottom "HP藥水:  " + getitemname(@autopot_hp_ID) + " (" + @autopot_hp_ID + ")";
		dispbottom "最低HP:   " + @autopot_hp_Min + " %";
	}
	if(.SP_Option){
		dispbottom "SP藥水:  " + getitemname(@autopot_sp_ID) + " (" + @autopot_sp_ID + ")";
		dispbottom "最低SP:   " + @autopot_sp_Min + " %";
	}
	dispbottom "延遲:    " + @autopot_delay + " 毫秒";
	dispbottom "---------------------------------------------";
	return;

L_Start:
	deltimer strnpcinfo(3) + "::OnStart";
	.@type$  = getarg(0, 0);
	.@potion = getarg(1, 0);
	setd "@autopot_" + .@type$ + "_Min", getarg(2, 0);
	@autopot_delay = getarg(3, .MinDelay);

	if (getd("@autopot_" + .@type$ + "_Min") < 1 || getd("@autopot_" + .@type$ + "_Min") > 100) setd "@autopot_" + .@type$ + "_Min", 90;
	if (@autopot_delay < .MinDelay) @autopot_delay = .MinDelay;
	if (@autopot_delay > .MaxDelay) @autopot_delay = .MaxDelay;
	if (getiteminfo(.@potion, ITEMINFO_TYPE) != IT_HEALING) {
		dispbottom getitemname(.@potion) + " 不是治療物品。";
		end;
	}
	if(inarray(.blackList[0], .@potion) != -1){
		message strcharinfo(0), "'" + getitemname(.@potion) + "' 已被列入黑名單，無法使用。";
		end;
	}
	if (BaseLevel < getiteminfo(.@potion, ITEMINFO_EQUIPLEVELMIN)) {
		message strcharinfo(0), "你的基礎等級太低，無法使用 '" + getitemname(.@potion) + "'。";
		end;
	}
	setd "@autopot_" + .@type$ + "_ID", .@potion;
	setd "@autopot_" + .@type$ + "_Active", true;
	addtimer @autopot_delay, strnpcinfo(3) + "::OnStart";
	message strcharinfo(0), "自動藥水 " + strtoupper(.@type$) + " 已啟用。";
	callsub L_Info;
	return;

OnCommand:
	if (!getarraysize(.@atcmd_parameters$)) {
		message strcharinfo(0), "語法錯誤。";
		callsub L_Help;
		end;
	}
	.@command$[0] = strtolower(.@atcmd_parameters$[0]);
	.@command$[1] = strtolower(.@atcmd_parameters$[1]);

	if(.@command$[0] == "hp" || .@command$[0] == "sp"){
		if((!.HP_Option && .@command$[0] == "hp") || (!.SP_Option && .@command$[0] == "sp")){
			message strcharinfo(0), "伺服器已禁用自動藥水 " + strtoupper(.@command$[0]) + " 功能。";
			end;
		}
		if(.@command$[1] == "on"){
			if(getd("@autopot_" + .@command$[0] + "_Active")){
				message strcharinfo(0), "自動藥水 " + strtoupper(.@command$[0]) + " 已經開啟。";
				end;
			}
			if(!getd("@autopot_" + .@command$[0] + "_ID")){
				dispbottom "你需要設定 " + strtoupper(.@command$[0]) + " 藥水。";
				callsub L_Help;
				end;
			}
			callsub L_Start, .@command$[0], getd("@autopot_" + .@command$[0] + "_ID");
			end;
		}
		if(.@command$[1] == "off"){
			message strcharinfo(0), "自動藥水 " + strtoupper(.@command$[0]) + " 已關閉。";
			setd "@autopot_" + .@command$[0] + "_Active", false;
			if(!@autopot_hp_Active || !@autopot_sp_Active)
				deltimer strnpcinfo(3) + "::OnStart";
			end;
		}
		if(.@command$[1] == "list"){
			getinventorylist;
			for(.@i = 0; .@i < @inventorylist_count; .@i++){
				if (getiteminfo(@inventorylist_id[.@i], ITEMINFO_TYPE) == IT_HEALING) {
					.@items[.@count] = @inventorylist_id[.@i];
					.@menu$ = .@menu$ + sprintf("~ ^0055FF%s^000000 (%dx)" + (inarray(.blackList[0], @inventorylist_id[.@i]) != -1? " ^364022列入黑名單^000000":"") + (BaseLevel < getiteminfo(@inventorylist_id[.@i], ITEMINFO_EQUIPLEVELMIN)? " ^E82C0C需要更高等級^000000":"") + ":", getitemname(@inventorylist_id[.@i]), countitem(@inventorylist_id[.@i]));
					.@count++;
				}
			}
			if(.@count){
				mes "[ 自動藥水 ]";
				mes "選擇 " + strtoupper(.@command$[0]) + " 物品。";
				.@select = select(.@menu$ + "   ^777777取消^000000") - 1;
				if (.@select != .@count)
					callsub L_Start, .@command$[0], .@items[.@select];
				close2;
			}else{
				message strcharinfo(0), "你的背包中沒有治療物品。";
			}
			end;
		}
		.@potion = atoi(.@command$[1]);
		.@percent = atoi(.@atcmd_parameters$[2]);
		.@delay = atoi(.@atcmd_parameters$[3]);
		if(getitemname(.@potion) != "null"){
			callsub L_Start, .@command$[0], .@potion, .@percent, .@delay;
			end;
		}
		message strcharinfo(0), "自動藥水：發生錯誤！";
		callsub L_Help;
	

	end;
	}
	if(.@command$[0] == "blacklist"){
		dispbottom "--------------黑名單物品--------------";
		for(.@i=0;.@i<getarraysize(.blackList); .@i++){
			dispbottom "    " + getitemname(.blackList[.@i]) + " (" + .blackList[.@i] + ")";
		}
		dispbottom "---------------------------------------------";
		end;
	}
	if(.@command$[0] == "info"){
		if(@autopot_hp_Active || @autopot_sp_Active){
			message strcharinfo(0), "自動藥水資訊如下。";
			callsub L_Info;
		}else{
			message strcharinfo(0), "自動藥水未啟用。";
		}
		end;
	}
	if(.@command$[0] == "help"){
		message strcharinfo(0), "指令列表如下。";
		callsub L_Help;
		end;
	}
	
	message strcharinfo(0), "語法錯誤。";
	callsub L_Help;
end;

OnStart:
	if (!getstatus(SC_BERSERK) && !getstatus(SC_SATURDAYNIGHTFEVER) && !getstatus(SC_GRAVITATION) &&
	    !getstatus(SC_TRICKDEAD) && !getstatus(SC_HIDING) && !getstatus(SC__SHADOWFORM) && !getstatus(SC__INVISIBILITY) &&
	    !getstatus(SC__MANHOLE) && !getstatus(SC_KAGEHUMI) && !getstatus(SC_HEAT_BARREL_AFTER) &&
		!getstatus(SC_STONE) && !getstatus(SC_FREEZE) && !getstatus(SC_STUN) && !getstatus(SC_SLEEP))
		{
		if(Hp){
			if(@autopot_hp_Active && Hp * 100 / MaxHp < @autopot_hp_Min) {
				if (countitem(@autopot_hp_ID)) {
					delitem @autopot_hp_ID, 1;
					consumeitem @autopot_hp_ID;
					specialeffect2 207;
				}
			}
			if(@autopot_sp_Active && Sp * 100 / MaxSp < @autopot_sp_Min) {
				if (countitem(@autopot_sp_ID)) {
					delitem @autopot_sp_ID, 1;
					consumeitem @autopot_sp_ID;
					specialeffect2 208;
				}
			}
		}
	}
	if(@autopot_hp_Active || @autopot_sp_Active)
		addtimer @autopot_delay, strnpcinfo(3) + "::OnStart";
end;
}
