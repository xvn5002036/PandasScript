//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script =======================================
//= Eden Group Quests - Mission [86 - 90]
//===== By: ==================================================
//= Chilly
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 86 - 90.
//===== Additional Comments: =================================
//= 1.0 First Version. [Chilly]
//= 1.1 Some little improvements here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//============================================================

moc_para01,48,175,3	script	任務 [86 - 90]	857,{
	if (countitem(6219) < 1) {
		mes "- 只有成為伊甸園的成員後， -";
		mes "- 才能閱讀這塊布告欄。 -";
		close;
	}
	if (BaseLevel < 86) {
		mes "- 這些任務只適合更高級別的冒險者。 -";
		mes "- 還是先去其他布告欄中找找合適我的任務吧！ -";
		close;
	}
	if (BaseLevel > 90) {
		setarray .@Exp[0],76000,82000,82000,72000,76000,72000,117000,82000,120000,72000,72000,72000,86000,80000;
		setarray .@Mob$[0],"刺尾蜻蜓","彈簧兔","單眼蟲","巫婆","鍾怪","芭芭亞卡","妖君","武士火槍兵","拉鏈熊","畢帝特地龍","草精","毒液魔","礦工魔","黃諾博斯";
		for(set .@i,0; .@i<14; set .@i,.@i+1)
			if (checkquest(4167+.@i,HUNTING) == 2) {
				mes "你已經狩獵完30隻"+.@Mob$[.@i]+"了。";
				mes "非常感謝！";
				next;
				mes "這是給你的小小禮物。";
				mes "儘管不多，但是希望能對你有所幫助。";
				next;
				erasequest 4167+.@i;
				getexp .@Exp[.@i],0;
				mes "哇哦~";
				mes "祝你好運~！";
				close;
			}
		mes "- 這些任務只適合對應級別的冒險家。 -";
		mes "- 我應該去其他的布告欄上看看有沒有我能做的任務。 -";
		close;
	}
	mes "- 布告欄上現有的狩獵任務： -";
	next;
	switch(select("龜島地區:鐘塔地區:本地區域:其它區域")) {
	case 1:
		switch(select("刺尾蜻蜓:彈簧兔:單眼蟲")) {
			case 1: callsub L_Quest,4167,"刺尾蜻蜓",76000;
			case 2: callsub L_Quest,4168,"彈簧兔",82000;
			case 3: callsub L_Quest,4169,"單眼蟲",82000;
		}
	case 2:
		switch(select("巫婆:鍾怪")) {
			case 1: callsub L_Quest,4170,"巫婆",72000;
			case 2: callsub L_Quest,4171,"鍾怪",76000;
		}
	case 3:
		switch(select("芭芭亞卡:妖君:武士火槍兵:拉鏈熊")) {
			case 1: callsub L_Quest,4172,"芭芭亞卡",72000;
			case 2: callsub L_Quest,4173,"妖君",117000;
			case 3: callsub L_Quest,4174,"武士火槍兵",82000;
			case 4: callsub L_Quest,4175,"拉鏈熊",120000;
		}
	case 4:
		switch(select("畢帝特地龍:草精:毒液魔:礦工魔:黃諾博斯")) {
			case 1: callsub L_Quest,4176,"畢帝特地龍",72000;
			case 2: callsub L_Quest,4177,"草精",72000;
			case 3: callsub L_Quest,4178,"毒液魔",72000;
			case 4: callsub L_Quest,4179,"礦工魔",86000;
			case 5: callsub L_Quest,4180,"黃諾博斯",80000;
		}
	}
	end;
L_Quest:
	if (checkquest(getarg(0),HUNTING) == 2) {
		mes "你已經狩獵完30隻"+getarg(1)+"了";
		mes "非常感謝！";
		next;
		mes "這是給你的小小禮物。";
		mes "儘管不多，但是希望能對你有所幫助。";
		next;
		erasequest getarg(0);
		getexp getarg(2),0;
		mes "哇哦~";
		mes "祝你好運~！";
		close;
	}
	if (checkquest(getarg(0)) == -1) {
		setquest getarg(0);
		mes "請狩獵完30隻"+getarg(1)+"後再返回這裡。";
		close;
	}
	mes "俄。。你還沒有完成呢。。";
	mes "需要狩獵30隻"+getarg(1)+"哦~";
	mes "要不你再檢查一下看看？";
	close;
}
