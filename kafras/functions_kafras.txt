//============================================================
//= rAthenaCN [卡普拉人員]漢化 
//============================================================

//===== rAthena Script =======================================
//= Kafra Functions
//===== By: ================================================== 
//= rAthena Dev Team
//= Darlskies, Darkchild, Syrus22, Lupus, kobra_k88 (2.0)
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 6.8
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: =========================================
//= These functions handle save, storage, cart rental, teleport,
//= and Free Teleport/Warp/Rent Cart options for all Kafra NPCs.
//=
//= Kafra's will need a lot of work. The Welcome Message argument
//= may become obsolete, as most kafras have a slightly differing
//= Welcome message.
//===== Additional Comments: ================================= 
//= 1.1 Now using functions v2.1 Added Cart Rent for Classes: Whitesmith, Professor.
//=     Replaced checkoption(x) into checkcart(0) [Lupus] v2.1b Added Fix Kafra Pass Func [Kobra_k88]
//= 2.2 Final fix of the Kafra Pass Exploit! [Lupus] a -Izlude[4] fix
//= 2.2a Minor changes to function calls.  Using agruments. Added Guild options. [kobra_k88]
//= 2.2b This version uses arrays for the teleport option. Rearranged next statements to make menu transitions smoother. [kobra_k88]
//= 2.3 Removed SAVE from Niflheim. [Lupus]
//= 2.3 removed "fix" by HawkMoon RTFM and check supernovice.txt . There's a SPECIAL Kafra which gives CARTS to SN for a special proce. [Lupus]
//= 2.4 Added Baby Class Support (Baby Novice check) Removed annoying storage feature where u had to close dialog window to be able to use your storage [Lupus]
//= 2.5 Added Louyang official Kafra, fixes some Kafras, not letting you to Save your position [Lupus]
//= 2.6 Reverted Dungeons Kafras (they should offer only Storage). Added temp Ayothaya Kafra
//= 2.7 Added correct Ayothaya, Louyang & Amatsu Kafras. [Lupus]
//= 2.8 Fixed Amatsu Storage problems [Lupus] 2.9 Fixed spelling mistakes. [Nexon]
//= 3.0 Added special "not working teleport menu" for Einbroch Kafras [Lupus]
//= 3.1 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 3.2 Fixed an exploit [Lupus]
//= 4.0 Added Kafra storage password protection. [Lupus]
//=     to block Kafra Password, read comments at 350 line
//= 5.0 Fixed the close2;....close; end; lines. Who let them slip away? =/ [erKURITA]
//=     Also, the kafra upon warp was giving back the zeny. Removed. [erKURITA]
//= 5.1 Optimized a little, added no tele/save arg's [Evera]
//= 5.1a Temporary? Added F_ClearGarbage to clear unused/outdated variables [Lupus]
//= 5.2 By default commented out custom Guilds Kafra's "Guild Storage". [Lupus]
//= 5.3 uncommented Guild Storage. Confirmed kRO feature. [Lupus]
//= 5.4 added -Guide option (Kafra shows you nearest Guide) Work in progress. 
//=     Need all coords for all guides.. and somehow pass their coords to kafra.
//=     Removed universal Kafra Pass, added 3 new Kafra Tickets [Lupus]
//= 5.4b temporary moved "-Guide" from the 1st menu punct [Lupus]
//= 5.5 Added proper Niflheim welcome message.	[L0ne_W0lf]
//=     Cleaned up the menus a bit. Got rid of the proceeding "-" prefix.
//= 5.5b Missed a preceeding hyphen, which was screwing up teleporting. [L0ne_W0lf]
//= 5.6 Further dialog updates, added  dynamic costs for cart and storage use. [L0ne_W0lf]
//=     Updated some functions to handle Guild Kafras the way they should. 
//= 5.6a Corrected a few wrong names in the teleport function. [L0ne_W0lf]
//= 5.7 Correced end message no longer displaying, and the duplicate names [L0ne_W0lf]
//=     Thanks to Barron-Monster for pointing out the issues.
//= 5.8 Fixed another double name being shown when "saving" [L0ne_W0lf]
//= 5.9 Fixed the Kafra Welcome message for guilds. Thanks Barron-Monster. [L0ne_W0lf]
//= 6.0 Closed Kafra Password exploit in Cool Event Corp. Storages until we got a client/packet based password support.
//=     Added 2 args into F_SetKafCode to fit it in Cool Evnt Co. NPC [Lupus]
//= 6.1 Added menu for Turbo Track Kafra Staff. [L0ne_W0lf]
//= 6.2 Updated/Fixed warp cords. [Kisuka]
//= 6.3 #kafra_code is now stored as is. [brianluau]
//= 6.4 Removed unofficial Guide function. [Daegaladh]
//= 6.5 Optimized. [Joseph]
//= 6.6 Added Warp Points / View Points Function. [Joseph]
//= 6.7 Added Rune Knight Kafra. [Euphy]
//= 6.8 Updated some functions and added VIP features. [Euphy]
//============================================================ 

//=== Main Function ========================================================
//=   arg(0): Used to determine which welcome message to show.
//=   arg(1): Used to determine which menu to display.
//=   arg(2): Used to determine if the info menu is shown in F_KafInfo.
//=   arg(3): Cost to use Storage
//=   arg(4): Cost to Rent a Pushcart
//==========================================================================
function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; // Clear outdated, unused variables
	.@welcome = getarg(0);
	.@menu_num = getarg(1);

	// Display Kafra Welcome Message
	switch(.@welcome){
		default:
		case 0:
			// Default message (obsolete)
			mes "[卡普拉僱員]";
			mes "---------------------------";
			mes "歡迎來到";
			mes "卡普拉服務中心.";
			mes "卡普拉的服務";
			mes "永遠在您身邊.有什麼可以幫助您的嗎?";
			break;
		case 1:
			// Niflheim specific message
			mes "[卡普拉僱員]";
			mes "^666666W-weeeelc-c-come";
			mes "to th-the K-kaaafrrrra";
			mes "C-coorpoor-r-ratioooonn...^000000";
			break;
		case 2:
			// Guild Castle Kafra message
			set @GID,getcharid(2);
			mes "[卡普拉僱員]";
	 		mes "歡迎光臨. ^ff0000" + GetGuildName(@GID) + "^000000 會員.";
			mes "卡普拉服務中心可以帶您去任何您想去的地方.";
			break;
		case 3:
			// Amatsu specific message (obsolete)
			mes "[卡普拉僱員]";
			mes "所以，您來自一個遙遠的土地來研究我們的文化，或者您只是觀光?";
			mes "在這種情況下, 為什麼不停留片刻?";
			mes "空氣中充滿了";
			mes "永恆的沉重與愉快的野花的香味.";
			break;		
		case 4:
			// Louyang and Ayothaya specific message (obsolete)
			mes "[卡普拉僱員]";
			mes "與我們許多的卡普拉服務人員";
			mes "服務區域, 您從未";
			mes "離開這個家.";
			break;		
		case 5:
			// NPC has it's own welcome message. (Display nothing)
			break;
	}
	next;
	while (1) {
		deletearray @K_Menu0$[0],getarraysize(@K_Menu0$);
		if (.@welcome == 2) {
			// Guild Kafra Menu override (free Teleport, free Storage, Free Cart)
			setarray @K_Menu0$[0],"- 使用倉庫服務","- 使用公會倉庫","- 手推車租賃服務","- 使用傳送服務","- 取消";
		} else {
			switch(.@menu_num){
				// Save and Storage only
				case 1:	setarray @K_Menu0$[0],"- 存儲服務","- 使用倉庫服務","- 取消"; break;
				// Storage only
				case 2:	setarray @K_Menu0$[0],"- 使用倉庫服務","- 取消"; break;
				// No Teleport (Common)
				case 3: setarray @K_Menu0$[0],"- 存儲服務","- 使用倉庫服務","- 手推車租賃服務","- 查詢其它情報信息","- 取消"; break;
				// Case 4 is Einbroch no tele message.
				// No save, or teleport. (Common)
				case 5:	setarray @K_Menu0$[0],"- 使用倉庫服務","- 手推車租賃服務","- 查詢其它情報信息","- 取消"; break;
				// Storage and Check Other Information only.
				case 6:	setarray @K_Menu0$[0],"- 使用倉庫服務","- 查詢其它情報信息","- 取消"; break;
				// Save, Storage, and Pushcart only (Kafra Warehouse)
				case 7:	setarray @K_Menu0$[0],"- 存儲服務","- 使用倉庫服務","- 手推車租賃服務","- 取消"; break;
				// Save, Storage, Other Check information. (Turbo track)
				case 8: setarray @K_Menu0$[0],"- 存儲服務","- 使用倉庫服務","- 查詢其它情報信息","- 取消"; break;
				// No Save (Rune Knight)
				case 9: setarray @K_Menu0$[0],"- 使用倉庫服務","- 手推車租賃服務","- 使用傳送服務","- 查詢其它情報信息","- 取消"; break;
				// Storage, Save, and Pushcart (Dewata, reorder of case 7)
				case 10: setarray @K_Menu0$[0],"- 使用倉庫服務","- 存儲服務","- 手推車租賃服務","- 取消"; break;
				// Default message (obsolete)
				default: setarray @K_Menu0$[0],"- 存儲服務","- 使用倉庫服務","- 打開公會倉庫","- 使用傳送服務","- 手推車租賃服務","- 查詢其它情報信息","- 取消"; break;
			}
		}
		set .@menu$,"";
		for (set .@i, 0; .@i < getarraysize(@K_Menu0$); set .@i, .@i + 1)
			set .@menu$, .@menu$ + @K_Menu0$[.@i] + ":";	
		set .@j, select (.@menu$) - 1;
		if (@K_Menu0$[.@j] == "- 存儲服務") {
			return;
		} else if (@K_Menu0$[.@j] == "- 使用倉庫服務") {
			// Do not charge for Guild Storage
			if(.@welcome == 2) 
				callfunc "F_KafStor",2,0,0;
			else 
				callfunc "F_KafStor",0,getarg(3),.@welcome;
			next;
		} else if (@K_Menu0$[.@j] == "- 使用傳送服務") {
			// Display Einbroch "沒有傳送服務" notice.
			if(.@menu_num != 4) callfunc "F_KafTele",.@welcome;
			else {
				mes "[卡普拉僱員]";
				mes "---------------------------";
				mes "因為 ^FF0000被限制";
				mes "根據傳送協定^000000, ";
				mes "卡普拉服務中心無法";
				mes "提供傳送服務在秀髮茲發德教國中.";
				next;
				mes "[卡普拉僱員]";
				mes "我們想請您用";
				mes "飛艇服務替代";
				mes "感謝您的";
				mes "理解和支持.";
			}
			next;
		} else if (@K_Menu0$[.@j] == "- 手推車租賃服務") {
			if(callfunc("F_KafCart",.@welcome,getarg(4)) == 1) 
				next;
		} else if (@K_Menu0$[.@j] == "- 查詢其它情報信息") {
			callfunc "F_KafInfo",getarg(2);
			next;
		} else if (@K_Menu0$[.@j] == "- 取消"){
			callfunc "F_KafEnd",.@welcome,0;
			end;
		} else if (@K_Menu0$[.@j] == "- 打開公會倉庫") {
			callfunc "F_KafStor",1,0;
			next;
		}
	}
}

//===================================================
// Storage Function
//===================================================
function	script	F_KafStor	{
	.@type = getarg(0);
	.@fee = getarg(1);

	// Unable to access Guild Storage (Busy)
	if(.@type == 1){
		if(guildopenstorage()){
			mes "[卡普拉僱員]";
			mes "對不起，公會有另外一位成員正在使用公會倉庫";
			mes "現在. 請等待另一位成員用完.";
			close2;
			cutin "", 255;
			end;
		}
		cutin "", 255;
		close;
	}
	// Unable to access Normal Storage (Insufficient Basic Skills)
	if(!callfunc("F_CanOpenStorage")){
		mes "[卡普拉僱員]";
		// Niflheim Specific Message
		if (getarg(2) == 1) {
			mes "^666666對…對……對不起,";
			mes "您…您是一個";
			mes "初心者... 需…需要";
			mes "基本職業技能等級 l-等級 6...^000000";
			return;
		}
		mes "對不起, 您";
		mes "需要基本職業";
		mes "技能等級 6才能";
		mes "使用倉庫服務.";
		return;
	}
	// Accessing Normal Storage (Skipped if accessing Storage from Guild castle)
	if(.@type != 2){
		// Consume "卡普拉倉庫免費使用券" if available.
		if(countitem(7059)) delitem 7059,1;
		else {
			if(Zeny<.@fee){
				mes "[卡普拉僱員]";
				// Niflheim Specific Message
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666zeny...";
					mes "您需要更多的zeny...!";
					mes "需要 150... zeny...";
					mes "Ergh! T-taking bl-blood~!^000000";
					return;
				}
				// Standard Message
				mes "對不起,但是您";
				mes "沒有足夠的 zeny 去使用";
				mes "倉庫服務. 我們";
				mes "倉庫使用費用需要 "+getarg(1)+" zeny.";
				return;
			}
			set Zeny, Zeny-.@fee;
			set RESRVPTS, RESRVPTS + (.@fee/5);
		}
	}

	// Surpress the "End" message if in guild castle.
	if (.@type != 2) {
		// Niflheim specific Message
		if (getarg(2) == 1) {
			percentheal 0,-10;
			mes "[卡普拉僱員]";
			for (set .@i, 0; .@i < 5; set .@i, .@i + 1)
				mes "^666666感謝您.. 的... 使用...^000000";
		}
		// Normal message
		else {
			mes "[卡普拉僱員]";
			mes "在這裡，讓我打開它";
			mes "您的倉庫.";
			mes "感謝您使用";
			mes "卡普拉服務.";
		}
	}
	callfunc("F_CheckKafCode");	//check your storage password, if set
	close2;
	openstorage;
	cutin "", 255;
	end;
}

//===================================================
// Teleport Function
//===================================================
function	script	F_KafTele	{
	mes "[卡普拉僱員]";
	mes "請選擇";
	mes "您的目的地.";
	next;
	set .@menu$, "";
	for (set .@i, 0; .@i <= 6; set .@i, .@i + 1)
		set .@menu$, .@menu$ + @wrpC$[.@i] + ":";
	set .@j, select (.@menu$) - 1;
	if (@wrpC$[.@j] == "- 取消") return;
	// Consume "卡普拉傳送服務免費使用券" if available.
	// Do not consume if in Guild castle
	if(countitem(7060) > 0 && getarg(0) != 2) delitem 7060,1;
	else {
		if (Zeny<@wrpP[.@j]){
			mes "[卡普拉僱員]";
			mes "對不起, 但是您沒有";
			mes "足夠的 zeny 使用";
			mes "傳送服務. 傳送服務的費用需要";
			mes "   "+@wrpD$[.@j]+" is "+@wrpP[.@j]+" zeny.";
			close2;
			cutin "", 255;
			end;
		}
		set Zeny, Zeny - @wrpP[.@j];
		set RESRVPTS, RESRVPTS + (@wrpP[.@j]/16);
	}
	cutin "", 255;
	if (@wrpD$[.@j] == "阿爾帕蘭") warp "aldebaran",168,112;
	else if (@wrpD$[.@j] == "阿爾貝塔") warp "alberta", 117, 56;
	else if (@wrpD$[.@j] == "克魔島") warp "comodo", 209, 143;
	else if (@wrpD$[.@j] == "克魔島法羅斯燈塔") warp "cmd_fild07", 127, 134;
	else if (@wrpD$[.@j] == "吉芬") warp "geffen", 120, 39;
	else if (@wrpD$[.@j] == "伊斯魯德") { if (checkre(0)) warp "izlude", 128, 98; else warp "izlude", 91, 105; }
	else if (@wrpD$[.@j] == "朱諾") warp "yuno", 158, 125;
	else if (@wrpD$[.@j] == "奧丁死者之坑") warp "mjolnir_02", 99, 351;
	else if (@wrpD$[.@j] == "夢羅克") warp "morocc", 156, 46;
	else if (@wrpD$[.@j] == "獸人洞穴") warp "gef_fild10", 52, 326;
	else if (@wrpD$[.@j] == "斐揚") warp "payon", 161, 58;
	else if (@wrpD$[.@j] == "普隆德拉") warp "prontera", 116, 72;
	else if (@wrpD$[.@j] == "汶巴拉") warp "umbala", 100, 154;
	end;
}


//===================================================
// Cart Function
//===================================================
function	script	F_KafCart	{
	// Ensure that the class wanting to rent a pushcart is a merchant
	if(BaseClass != Job_Merchant){
		mes "[卡普拉僱員]";
		mes "對不起, 但是";
		mes "手推車租賃服務";
		if (checkre(0))
			mes "只開放給商人系列職業.";
		else {
			mes "只開放給商人系列職業,";
			mes "例如銀匠, 鍊金術士等.";
			mes "和超級初心者的.";
		}
		return 1;
	}
	// Make sure the invoking character does not have a cart already
	else if(checkcart() == 1){
		mes "[卡普拉僱員]";
		mes "您已經擁有了";
		mes "一輛手推車.";
		mes "不幸的是, 我們不能";
		mes "在同一時間";
		mes "不能租賃更多的給顧客.";
		return 1;
	}
	// Check if the player has the Pushcart skill
	else if(getskilllv("MC_PUSHCART") == 0) {
		mes "[卡普拉僱員]";
		mes "You can only rent a cart after learning the \"Push Cart\" skill.";
		return 1;
	}
	// Consume "卡普拉手推車服務免費使用券" if available.
	// Do not consume if in Guild castle.
	if(countitem(7061) > 0 && getarg(0) != 2) delitem 7061,1;
	else {
		mes "[卡普拉僱員]";
		mes "手推車租賃, 的費用需要.";
		mes ""+getarg(1)+" zeny. ";
		mes "您想租一輛手推車嗎?";
		next;
		if (select("- 租一輛手推車.:- 取消") == 2) return 0;
			if(Zeny<getarg(1)){
				mes "[卡普拉僱員]";
				mes "對不起, 但是您";
				mes "沒有足夠的";
				mes "zeny 去支付租賃手推車";
				mes "所需要的 "+getarg(1)+" zeny.";
				return 1;
			}
			set Zeny,Zeny-getarg(1);
			set RESRVPTS, RESRVPTS + 48;
	}
	setcart;
	return 1;
}

//===================================================
// Special Reserve Points Function
//===================================================
function	script	F_KafInfo	{
	// Uncomment next line to block Kafra Storage Protection
	//	set .@block, 1;
		setarray .@m$, "- 查詢特別準備金","- 倉庫密碼服務","- 卡普拉職員分布位置","- 取消";
		if (getarg(0) == 2) deletearray .@m$[1],2; // Port Malaya, Rune Knight
		else if (.@block) deletearray .@m$[1],1;
	while (1) {
		set .@j, select(implode(.@m$,":")) - 1;
		if (.@m$[.@j] == "- 查詢特別準備金") {
			mes "[卡普拉僱員]";
			mes "讓我們看看...";
			mes strcharinfo(0) + "...";
			mes "啊, 您總共有";
			mes RESRVPTS+ " 特別準備金.";
			next;
			mes "[卡普拉僱員]";
			mes "您可以用這些點數";
			mes "特別準備金 ";
			mes "在艾爾帕蘭的卡普拉主要辦公室領取獎勵.";
			mes "請使用我們的方便服務，看到我們的獎勵計劃的好處.";
			next;
			if(getarg(0) == 1) return;
		} else if (.@m$[.@j] == "- 卡普拉職員分布位置") {
			for (set .@i, 0; .@i <= 3; set .@i, .@i + 1)
				viewpoint 1,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			next;
			for (set .@i, 0; .@i <= 3; set .@i, .@i + 1)
				viewpoint 2,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
		} else if (.@m$[.@j] == "- 倉庫密碼服務") {
			callfunc("F_SetKafCode","[卡普拉僱員]","卡普拉服務");
		} else {
			return;
		}
	}
}

// === End Function ==================================================
//  arg(0): used to determine what message to display.
//  arg(1): used to determine if save message is displayed.
//  arg(2): used to display the name of the area you're saving in.
//===================================================================
function	script	F_KafEnd	{
	mes "[卡普拉僱員]";
	if(getarg(1)==1) {	
		// Save
		mes "您的重生地點";
		mes "現在被保存在這裡";
		mes getarg(2)+".";
		mes "感謝您使用";
		mes "卡普拉服務.";
	} else if(getarg(0)==0 || getarg(0)==5) {	
		// Generic End
		mes "我們卡普拉服務中心,";
		mes "一直在努力為您提供最好的服務.我們希望滿足您的冒險的需要和優秀的標準.";
	} else if(getarg(0)==1) {
		// Niflheim End
		percentheal 0,- 25;
		mes "^666666卡普拉服務人員";
		mes "一直";
		mes "在您的身邊~^000000";
	}
	close2;
	cutin "", 255;
	end;
}

//===================================================
// Check Storage Password Function
//===================================================
function	script	F_CheckKafCode	{
	if(!#kafra_code) return;
	mes "請輸入您的倉庫密碼:";
	set @code_,0;
	input @code_;
	if(@code_ != #kafra_code) {
		dispbottom "密碼錯誤.";
		close2;
		cutin "",255;
		end;
	}
	set @kafcode_try,0;
	set @code_,0;
	return;
}

// === Set / Change / Clear Storage Password Function ===
// getarg(0): NPC Name
// getarg(1): Company Name
//=======================================================
function	script	F_SetKafCode	{
	.@npc_name$ = getarg(0);
	.@comp_name$ = getarg(1);

	mes .@npc_name$;
	if(#kafra_code) {
		mes "您的倉庫現在有密碼保護了.您現在還想做什麼?";
		next;
		switch (select("- 修改密碼 -> 5000z:- 刪除倉庫密碼 -> 1000z:- 取消")) {
			case 1:
				mes .@npc_name$;
				mes "首先, 請您輸入 ^0000FF舊密碼^000000.";
				set @code,callfunc("F_EntKafCode");
				if(!@code || @code != #kafra_code) {
					mes "密碼錯誤. 您無法修改密碼，請重新操作.";
					emotion ET_SCRATCH;
					break;
				}
				next;
				callsub S_SET,.@npc_name$,.@comp_name$;
				break;
			case 2:
				mes .@npc_name$;
				mes "請輸入您的倉庫密碼.";
				set @code,callfunc("F_EntKafCode");
				if(!@code) {
					mes "密碼未被刪除.";
					emotion ET_SCRATCH;
					break;
				}
				next;
				mes .@npc_name$;
				if(Zeny < 1000) {
					mes "您沒有足夠的zeny.";
					emotion ET_MONEY;
					break;
				}
				set Zeny,Zeny-1000;
				if(@code == #kafra_code) {
					set #kafra_code,0;
					mes "您已成功清除了您的舊密碼.";
					mes "感謝您使用 "+getarg(1)+".";
					emotion ET_THANKS;
				} else {
					mes "密碼錯誤. 我們不會歸還您的 1000z.";
					mes "請輸入正確的密碼，再試一次.";
					emotion ET_SORRY;
				}
				break;
			case 3:
				break;
		}
	} else {
		mes .@comp_name$+" 自豪地介紹您一個新的服務:";
		mes "為您的倉庫增加密碼保護.";
		next;
		switch (select("- 請設定一個新密碼 -> 5000z:- 取消")) {
			case 1: callsub S_SET,.@npc_name$,.@comp_name$; break;
			case 2: break;		
		}
	}
	close2;
	cutin "",255;
	end;

S_SET:
	mes .@npc_name$;
	mes "請輸入 ^FF0000新密碼^000000 以防止倉庫被盜.";
	set @code,callfunc("F_EntKafCode");
	if(!@code) {
		mes "密碼沒有改變.";
		emotion ET_SCRATCH;
		return;
	}
	next;
	mes .@npc_name$;
	if(Zeny < 5000) {
		mes "您沒有足夠的Zeny.";
		emotion ET_MONEY;
		return;
	}
	set Zeny,Zeny-5000;
	set #kafra_code,@code;
	mes "您已經擁有了一個密碼來保護您的倉庫.";
	mes "感謝您的使用 "+getarg(1)+".";
	emotion ET_THANKS;
	return;
}

//===================================================
// Basic Password Validation Function
//===================================================
function	script	F_EntKafCode	{
	mes "請輸入一個1000~10000000之間的數字:";
	set @code_,0;
	set @kafcode_try,@kafcode_try+1;
	if(@kafcode_try>10) {
		set @kafcode_try,0;
		logmes "破解: 嘗試以符合倉庫密碼.";
	}
	if(input(@code_) == 1) {
		mes "您的密碼太長.";
		return 0;
	}
	if(@code_<1000) {
		mes "您的密碼太短.";
		return 0;
	}
	return @code_;
}

//===================================================
// Warp Points / View Points Function
//===================================================
function	script	F_KafSet	{
	cleararray @wrpC$[0],"",getarraysize(@wrpC$);
	cleararray @wrpD$[0],"",getarraysize(@wrpD$);
	cleararray @wrpP$[0],"",getarraysize(@wrpP$);
	cleararray @viewpX[0],"",getarraysize(@viewpX);
	cleararray @viewpY[0],"",getarraysize(@viewpY);
	if (strnpcinfo(4) == "prontera") {
		setarray @wrpD$[0], "伊斯魯德", "吉芬", "斐揚", "夢羅克", "獸人洞穴", "阿爾貝塔";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1700, 1800;
		setarray @viewpX[0], 151, 29, 282, 152;
		setarray @viewpY[0], 29, 207, 200, 326;
	} else if (strnpcinfo(4) == "alberta") {
		setarray @wrpP[0], 1200, 1800, 1800;
		setarray @wrpD$[0], "斐揚", "夢羅克", "普隆德拉";
		setarray @viewpX[0], 28, 113, 0, 0;
		setarray @viewpY[0], 229, 60, 0, 0;
	} else if (strnpcinfo(4) == "aldebaran") {
		setarray @wrpP[0], 1200, 1200, 1800, 1700;
		setarray @wrpD$[0], "吉芬", "朱諾" , "伊斯魯德", "雷神死者之坑";
	} else if (strnpcinfo(4) == "comodo" ) {
		setarray @wrpD$[0], "夢羅克", "克魔島法羅斯燈塔", "汶巴拉";
		setarray @wrpP[0], 1800, 1200, 1800;
	} else if (strnpcinfo(4) == "cmd_fild07") {
		setarray @wrpD$[0], "克魔島", "夢羅克";
		setarray @wrpP[0], 1200, 1200;
	} else if (strnpcinfo(4) == "geffen") {
		setarray @wrpD$[0], "普隆德拉", "阿爾帕蘭", "獸人洞穴", "雷神死者之坑";
		setarray @wrpP[0], 1200, 1200, 1700, 1700;
		setarray @viewpX[0], 120, 203;
		setarray @viewpY[0], 62, 123;
	} else if (strnpcinfo(4) == "izlude") {
		setarray @wrpD$[0], "吉芬", "斐揚", "夢羅克", "阿爾帕蘭";
		setarray @wrpP[0], 1200, 1200, 1200, 1800;
	} else if (strnpcinfo(4) == "morocc") {
		setarray @wrpD$[0], "普隆德拉", "斐揚", "阿爾貝塔", "克魔島", "克魔島法羅斯燈塔";
		setarray @wrpP[0], 1200, 1200, 1800, 1800, 1200;
		setarray @viewpX[0], 156, 163, 28, 292;
		setarray @viewpY[0], 97, 260, 167, 211;
	} else if (strnpcinfo(4) == "umbala") {
		setarray @wrpD$[0], "克魔島";
		setarray @wrpP[0], 1800;
	} else if (strnpcinfo(4) == "payon") {
		setarray @wrpD$[0], "普隆德拉", "阿爾貝塔", "夢羅克";
		setarray @wrpP[0], 1200, 1200, 1200;
	} else if (strnpcinfo(4) == "yuno") {
		setarray @wrpD$[0], "阿爾帕蘭";
		setarray @wrpP[0], 1200;
		setarray @viewpX[0], 328, 278, 153, 0;
		setarray @viewpY[0], 108, 221, 187, 0;
	} else if (strnpcinfo(4) == "job3_rune01") {
		setarray @wrpD$[0], "伊斯魯德", "吉芬", "斐揚", "夢羅克", "阿爾貝塔";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1800;
	}

	// VIP pricing varies across the regional servers; this is most practical.
	if (VIP_SCRIPT && !vip_status(1)) {
		for (set .@i, 0; .@i < getarraysize(@wrpD$); set .@i, .@i + 1)
			setd "@wrpP["+.@i+"]",getd("@wrpP["+.@i+"]") * 2;
	}

	for (set .@i, 0; .@i < getarraysize(@wrpD$); set .@i, .@i + 1)
		setd "@wrpC$["+.@i+"]",getd("@wrpD$["+.@i+"]")+" -> "+getd("@wrpP["+.@i+"]")+" z";
	setd "@wrpC$["+getarraysize(@wrpC$)+"]","- 取消";
	return;
}
